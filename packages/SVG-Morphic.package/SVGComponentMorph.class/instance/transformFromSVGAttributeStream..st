transformation
transformFromSVGAttributeStream: str
	"Answer a new MatrixTransform2x3 for the transform (sub)attribute."

	|matrix a x y type|
	matrix := MatrixTransform2x3 identity.
	type := str upTo: $(.
	(type = 'matrix')
		ifTrue: [matrix
				a11: str nextNumber;
				a21: str nextNumber;
				a12: str nextNumber;
				a22: str nextNumber;
				a13: str nextNumber;
				a23: str nextNumber].
	(type = 'translate')
		ifTrue: [x := str nextNumber.
				y := str nextNumber.
				y ifNil: [y := 0].
				matrix
					setOffset: x@y].
	(type = 'scale')
		ifTrue: [x := str nextNumber.
				y := str nextNumber.
				y ifNil: [y := x].				
				matrix
					setScale: x@y].
	(type = 'rotate')
		ifTrue: [a  := str nextNumber.
				x := str nextNumber.
				y := str nextNumber.	
				matrix
					setAngle: a.
				(x notNil and: [y notNil])
					ifTrue: [matrix := ((MatrixTransform2x3 withOffset: x@y)
									composedWithLocal: matrix)
									composedWithLocal: (MatrixTransform2x3 withOffset: x negated@y negated)]].
							
	"The X and Y may seem switched here, however, this is correct for SVG parsing"
	(type = 'skewX')
		ifTrue: [	a := str nextNumber.		
				matrix
					setSkew: 0@a].
	(type = 'skewY')
		ifTrue: [	a := str nextNumber.		
				matrix
					setSkew: a@0].
	^matrix